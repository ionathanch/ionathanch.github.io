---
layout: post
title: "An Analysis of An Analysis of Girard's Paradox"
excerpt_separator: "<!--more-->"
tags:
  - type theory
  - inconsistency
---

While it's rather difficult to accidentally prove an inconsistency in a well-meaning type theory that isn't obviously inconsistent
(have you ever unintentionally proven that a type corresponding to an ordinal is strictly larger than itself? I didn't think so),
it feels like it's comparatively easy to add rather innocent features to your type theory that will suddenly make it inconsistent.
And there are so many of them!
And sometimes it's the *interaction* among the features rather than the features themselves that produce inconsistencies.

<!--more-->

## Hurken's Paradox

As it turns out, a lot of the inconsistencies can surface as proofs of what's known as Hurkens' paradox [[1](#1)],
which is a simplification of Girard's paradox [[2](#2)],
which itself is a type-theoretical formulation of the set-theoretical Buraliâ€“Forti's paradox [[3](#3)].
I won't claim to deeply understand how any of these paradoxes work,
but I'll present various formulations of Hurkens' paradox in the context of the most well-known inconsistent features.

### Type in Type

The most common mechanization of Hurkens' paradox you can find online is using type-in-type,
where the type of the universe `Type` has `Type` itself as its type,
because most proof assistants have ways of turning this check off.
We begin with what Hurkens calls a _powerful paradoxical universe_,
which is a type `U` along with two functions `Ï„ : â„˜ (â„˜ U) â†’ U` and `Ïƒ : U â†’ â„˜ (â„˜ U)`.
Conceptually, `â„˜ X` is the powerset of `X`, implemented as `X â†’ Type`;
`Ï„` and `Ïƒ` then form an isomorphism between `U` and the powerset of its powerset, which is an inconsistency.
Hurkens defines `U`, `Ï„`, and `Ïƒ` as follows, mechanized in Agda below.

```
U : Set
U = âˆ€ (X : Set) â†’ (â„˜ (â„˜ X) â†’ X) â†’ â„˜ (â„˜ X)

Ï„ : â„˜ (â„˜ U) â†’ U
Ï„ t = Î» X f p â†’ t (Î» x â†’ p (f (x X f)))

Ïƒ : U â†’ â„˜ (â„˜ U)
Ïƒ s = s U Ï„
```

The complete proof can be found at [Hurkens.html](/assets/agda/Hurkens.html),
but we'll focus on just these definitions for the remainder of this post.

### Two Impredicative Universe Layers

Hurkens' original construction of the paradox was done in System Uâ», where there are _two_ impredicative universes,
there named `*` and `â–¡`.
We'll call ours `Set` and `Setâ‚`, with the following typing rules for function types featuring impredicativity.

```
Î“ âŠ¢ A : ğ’°
Î“, x: A âŠ¢ B : Set
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Î -Set
Î“ âŠ¢ Î x: A. B : Set

Î“ âŠ¢ A : ğ’°
Î“, x: A âŠ¢ B : Setâ‚
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Î -Setâ‚
Î“ âŠ¢ Î x: A. B : Setâ‚
```

Going back to the type-in-type proof, consider now `â„˜ (â„˜ X)`.
By definition, this is `(X â†’ Set) â†’ Set`; since `Set : Setâ‚`, by Î -Setâ‚,
the term has type `Setâ‚`, regardless of what the type of `X` is.
Then `U = âˆ€ X â†’ (â„˜ (â„˜ X) â†’ X) â†’ â„˜ (â„˜ X)` has type `Setâ‚` as well.
Because later when defining `Ïƒ : U â†’ â„˜ (â„˜ U)`, given a term `s : U`, we want to apply it to `U`,
the type of `X` should have the same type as `U` for `Ïƒ` to type check.
The remainder of the proof of inconsistency is unchanged, as it doesn't involve any explicit universes,
although we also have the possibility of lowering the return type of `â„˜`.
An impredicative `Setâ‚` above a predicative `Set` may be inconsistent as well,
since we never make use of the impredicativity of `Set` itself.

```
â„˜ : âˆ€ {â„“} â†’ Set â„“ â†’ Setâ‚
â„˜ {â„“} S = S â†’ Set

U : Setâ‚
U = âˆ€ (X : Setâ‚) â†’ (â„˜ (â„˜ X) â†’ X) â†’ â„˜ (â„˜ X)
```

Note well that having two impredicative universe layers is _not_ the same thing as having two parallel impredicative universes.
For example, by turning on `-impredicative-set` in Coq, we'd have an impredicative `Prop` and an impredicative `Set`,
but they are in a sense parallel universes: the type of `Prop` is `Type`, not `Set`.
The proof wouldn't go through in this case, since it relies on the type of the return type of `â„˜` being impredicative as well.
With cumulativity, `Prop` is a subtype of `Set`, but this has no influence for our puposes.

### Strong Impredicative Pairs

A _strong (dependent) pair_ is a pair from which we can project its components.
An _impredicative pair_ in some impredicative universe `ğ’°` is a pair that lives in `ğ’°` when either of its components live in `ğ’°`,
regardless of the universe of the other component.
It doesn't matter too much which specific universe is impredicative as long as we can refer to both it and its type,
so we'll suppose for this section that `Set` is impredicative.
The typing rules for the strong impredicative pair are then as follows;
we only need to allow the first component of the pair to live in any universe.

```
Î“ âŠ¢ A : ğ’°
Î“, x: A âŠ¢ B : Set
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ Î£x: A. B : Set

Î“ âŠ¢ a : A
Î“ âŠ¢ b : B[x â†¦ a]
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ (a, b) : Î£x: A. B

Î“ âŠ¢ p : Î£x: A. B
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ fst p : A

Î“ âŠ¢ p : Î£x: A. B
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ snd p : B[x â†¦ fst p]

Î“ âŠ¢ (a, b) : Î£x: A. B
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ fst (a, b) â‰¡ a : A

Î“ âŠ¢ (a, b) : Î£x: A. B
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ snd (a, b) â‰¡ b : B[x â†¦ a]
```

If we turn type-in-type off in the previous example, the first place where type checking fails is for `U`,
which with predicative universes we would expect to have type `Setâ‚`.
The idea, then, is to squeeze `U` into the lower universe `Set` using the impredicativity of the pair,
then to extract the element of `U` as needed using the strongness of the pair.
Notice that we don't actually need the second component of the pair, which we can trivially fill in with `âŠ¤`.
This means we could instead simply use the following record type in Agda.

```
record Lower (A : Setâ‚) : Set where
  constructor lower
  field raise : A
```

The type `Lower A` is equivalent to `Î£x: A. âŠ¤`, its constructor `lower a` is equivalent to `(a, tt)`,
and the projection `raise` is equivalent to `fst`.
To allow type checking this definition, we need to again turn on type-in-type, despite never actually exploiting it.
If we really want to make sure we really never make use of type-in-type,
we can postulate `Lower`, `lower`, and `raise`, and use rewrite rules to recover the computational behaviour of the projection.

```
{-# OPTIONS --rewriting #-}

postulate
  Lower : (A : Setâ‚) â†’ Set
  lower : âˆ€ {A} â†’ A â†’ Lower A
  raise : âˆ€ {A} â†’ Lower A â†’ A
  beta : âˆ€ {A} {a : A} â†’ raise (lower a) â‰¡ a

{-# REWRITE beta #-}
```

Refactoring the existing proof is straightforward:
any time an element of `U` is used, it must first be raised back to its original universe,
and any time an element of `U` is produced, it must be lowered down to the desired universe.

```
U : Set
U = Lower (âˆ€ (X : Set) â†’ (â„˜ (â„˜ X) â†’ X) â†’ â„˜ (â„˜ X))

Ï„ : â„˜ (â„˜ U) â†’ U
Ï„ t = lower (Î» X f p â†’ t (Î» x â†’ p (f (raise x X f))))

Ïƒ : U â†’ â„˜ (â„˜ U)
Ïƒ s = raise s U Ï„
```

Again, the complete proof can be found at [HurkensLower.html](/assets/agda/HurkensLower.html).
One final thing to note is that impredicativity (with respect to function types) of `Set` isn't used either;
all of this code type checks in Agda, whose universe `Set` is not impredicative.
This means that impredicativity with respect to strong pair types alone is sufficient for inconsistency.

### Unrestricted Large Elimination of Impredicative Universes

In contrast to strong pairs, weak (impredicative) pairs don't have first and second projections.
Instead, to use a pair, one binds its components in the body of some expression
(continuing our use of an impredicative `Set`).

```
Î“ âŠ¢ p : Î£x: A. B
Î“, x: A, y: B âŠ¢ e : C
Î“ âŠ¢ C : Set
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ let (x, y) := p in e : C
```

The key difference is that the type of the expression must live in `Set`, and not in any arbitrary universe.
Therefore, we can't generally define our own first projection function, since `A` might not live in `Set`.

Weak impredicative pairs can be generalized to inductive types in an impredicative universe,
where the restriction becomes disallowing arbitrary _large elimination_ to retain consistency.
This appears in the typing rule for case expressions on inductives.

```
Î“ âŠ¢ t : I pâ€¦ aâ€¦
Î“ âŠ¢ I pâ€¦ : (y: u)â€¦ â†’ ğ’°
Î“, y: u, â€¦, x: I pâ€¦ aâ€¦ âŠ¢ P : ğ’°'
elim(ğ’°, ğ’°') holds
< other premises omitted >
â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
Î“ âŠ¢ case t return Î»yâ€¦. Î»x. P of [c xâ€¦ â‡’ e]â€¦ : P[yâ€¦ â†¦ aâ€¦][x â†¦ t]
```

The side condition `elim(ğ’°, ğ’°')` holds if:
* `ğ’° = Setâ‚` or higher; or
* `ğ’° = ğ’°' = Set`; or
* `ğ’° = Set` and
  * Its constructors' arguments are either forced or have types living in `Set`; and
  * The fully-applied constructors have orthogonal types; and
  * Recursive appearances of the inductive type in the constructors' types are syntactically guarded.

The three conditions of the final case come from the rules for definitionally proof-irrelevant `Prop` [[4](#4)];
the conditions that Coq uses are that the case target's inductive type must be a singleton or empty,
which is a subset of those three conditions.
As the pair constructor contains a non-forced, potentially non-`Set` argument in the first component,
impredicative pairs can only be eliminated to terms whose types are in `Set`,
which is exactly what characterizes the weak impredicative pair.
On the other hand, allowing unrestricted large elimination lets us define not only strong impredicative pairs,
but also `Lower` (and the projection `raise`), both as inductive types.

While impredicative functions can Church-encode weak impredicative pairs, they can't encode strong ones.

```
Î£x: A. B â‰ (P : Set) â†’ ((x : A) â†’ B â†’ P) â†’ P
```

If `Set` is impredicative then the pair type itself lives in `Set`,
but if `A` lives in a larger universe, then it can't be projected out of the pair,
which requires setting `P` as `A`.

## Other Paradoxes

There's a variety of other features that yield inconsistencies in other ways,
some of them resembling the set-theoretical Russell's paradox.

### Negative Inductive Types

A negative inductive type is one where the inductive type appears to the left of an odd number of arrows in a constructor's type.
For instance, the following definition will allow us to derive an inconsistency.

```
record Bad : Set where
  constructor mkBad
  field bad : Bad â†’ âŠ¥
open Bad
```

The field of a `Bad` essentially contains a negation of `Bad` itself (and I believe this is why this is considered a "negative" type).
So when given a `Bad`, applying it to its own field, we obtain its negation.

```
notBad : Bad â†’ âŠ¥
notBad b = b.bad b
```

Then from the negation of `Bad` we construct a `Bad`, which we apply to its negation to obtain an inconsistency.

```
bottom : âŠ¥
bottom = notBad (mkBad notBad)
```

### Positive Inductive Types

_This section is adapted from [Why must inductive types be strictly positive?](http://vilhelms.github.io/posts/why-must-inductive-types-be-strictly-positive/)_.

A positive inductive type is one where the inductive type appears to the left of an even number of arrows in a constructor's type.
(Two negatives cancel out to make a positive, I suppose.)
If it's restricted to appear to the left of _no_ arrows (0 is an even number), it's a _strictly_ positive inductive type.
Strict positivity is the usual condition imposed on all inductive types in Coq.
If instead we allow positive inductive types in general, when combined with an impredicative universe (we'll use `Set` again),
we can define an inconsistency corresponding to Russell's paradox.

```
{-# NO_POSITIVITY_CHECK #-}
record Bad : Setâ‚ where
  constructor mkBad
  field bad : â„˜ (â„˜ Bad)
```

From this definition, we can prove an injection from `â„˜ Bad` to `Bad` via an injection from `â„˜ Bad` to `â„˜ (â„˜ Bad)`
defined as a partially-applied equality type.

```
f : â„˜ Bad â†’ Bad
f p = mkBad (_â‰¡ p)

fInj : âˆ€ {p q} â†’ f p â‰¡ f q â†’ p â‰¡ q
fInj {p} fpâ‰¡fq = subst (Î» pâ‰¡ â†’ pâ‰¡ p) (badInj fpâ‰¡fq) refl
  where
  badInj : âˆ€ {a b} â†’ mkBad a â‰¡ mkBad b â†’ a â‰¡ b
  badInj refl = refl
```

Evidently an injection from a powerset of some `X` to `X` itself should be an inconsistency.
However, it doesn't appear to be provable without using some sort of impredicativity.
(We'll see.)
Coquand and Paulin [[5](#5)] use the following definitions in their proof, which does not type check without type-in-type,
since `â„˜ Bad` otherwise does not live in `Set`.
In this case, weak impredicative pairs would suffice, since the remaining definitions can all live in the same impredicative universe.

```
Pâ‚€ : â„˜ Bad
Pâ‚€ x = Î£[ P âˆˆ â„˜ Bad ] x â‰¡ f P Ã— Â¬ (P x)

xâ‚€ : Bad
xâ‚€ = f Pâ‚€
```

From here, we can prove `Pâ‚€ xâ‚€ â†” Â¬ Pâ‚€ xâ‚€`. The rest of the proof can be found at [Positivity.html](/assets/agda/Positivity.html).

### Impredicativity + Excluded Middle + Large Elimination

Another type-theoretic encoding of Russell's paradox is Berardi's paradox [[6](#6)].
It begins with a retraction, which looks like half an isomorphism.

```
record _â—_ {â„“} (A B : Set â„“) : Set â„“ where
  constructor _,_,_
  field
    Ï• : A â†’ B
    Ïˆ : B â†’ A
    retract : Ïˆ âˆ˜ Ï• â‰¡ id
open _â—_
```

We can easily prove `A âŠ² B â†’ A âŠ² B` by identity.
If we postulate the axiom of choice, then we can push the universal quantification over `A âŠ² B` into the existential quantification of `A âŠ² B`,
yielding a `Ï•` and a `Ïˆ` such that `Ïˆ âˆ˜ Ï• â‰¡ id` only when given some proof of `A âŠ² B`.
However, a retraction of powersets can be stipulated out of thin air using only the axiom of excluded middle.

```
record _â—â€²_ {â„“} (A B : Set â„“) : Set â„“ where
  constructor _,_,_
  field
    Ï• : A â†’ B
    Ïˆ : B â†’ A
    retract : A â— B â†’ Ïˆ âˆ˜ Ï• â‰¡ id
open _â—â€²_

postulate
  EM : âˆ€ {â„“} (A : Set â„“) â†’ A âŠ (Â¬ A)

t : âˆ€ {â„“} (A B : Set â„“) â†’ â„˜ A â—â€² â„˜ B
t A B with EM (â„˜ A â— â„˜ B)
... | injâ‚  â„˜Aâ—â„˜B =
      let Ï• , Ïˆ , retract = â„˜Aâ—â„˜B
      in Ï• , Ïˆ , Î» _ â†’ retract
... | injâ‚‚ Â¬â„˜Aâ—â„˜B =
      (Î» _ _ â†’ âŠ¥) , (Î» _ _ â†’ âŠ¥) , Î» â„˜Aâ—â„˜B â†’ âŠ¥-elim (Â¬â„˜Aâ—â„˜B â„˜Aâ—â„˜B)

```

This time defining `U` to be `âˆ€ X â†’ â„˜ X`, we can show that `â„˜ U` is a retract of `U`.
Here, we need an impredicative `Set` so that `U` can also live in `Set` and so that `U` quantifies over itself as well.
Note that we project the equality out of the record while the record is impredicative,
so putting `_â‰¡_` in `Set` as well will help us avoid large eliminations for now.

```
projáµ¤ : U â†’ â„˜ U
projáµ¤ u = u U

injáµ¤ : â„˜ U â†’ U
injáµ¤ f X =
  let _ , Ïˆ , _ = t X U
      Ï• , _ , _ = t U U
  in Ïˆ (Ï• f)

projáµ¤âˆ˜injáµ¤ : projáµ¤ âˆ˜ injáµ¤ â‰¡ id
projáµ¤âˆ˜injáµ¤ = retract (t U U) (id , id , refl)
```

Now onto Russell's paradox.
Defining `_âˆˆ_` to be `projáµ¤` and letting `r â‰ injáµ¤ (Î» u â†’ Â¬ u âˆˆ u)`,
we can show a curious inconsistent statement.

```
râˆˆrâ‰¡râˆ‰r : r âˆˆ r â‰¡ (Â¬ r âˆˆ r)
râˆˆrâ‰¡râˆ‰r = cong (Î» f â†’ f (Î» u â†’ Â¬ u âˆˆ u) r) projáµ¤âˆ˜injáµ¤
```

To actually derive an inconsistency, we can derive functions `r âˆˆ r â†’ (Â¬ r âˆˆ r)` and `(Â¬ r âˆˆ r) â†’ r âˆˆ r` using substitution,
then prove falsehood the same way we did for negative inductive types.
However, the predicate in the substitution is `Set â†’ Set`, which itself has type `Setâ‚`,
so these final steps do require unrestricted large elimination.
The complete proof can be found at [Berardi.html](/assets/agda/Berardi.html).

### Unrestricted Large Elimination (again)

Having impredicative inductive types that can be eliminated to large types can yield an inconsistency
without having to go through Hurkens' paradox.
To me, at least, this inconsistency is a lot more comprehensible.
This time, we use an impredicative representation of the ordinals [[7](#7)],
prove that they are well-founded with respect to some reasonable order on them,
then prove a falsehood by providing an ordinal that is obviously *not* well-founded.
This representation can be type checked using Agda's `NO_UNIVERSE_CHECK` pragma,
and normally it would live in `Setâ‚` due to one constructor argument type living in `Setâ‚`.

```
{-# NO_UNIVERSE_CHECK #-}
data Ord : Set where
  â†‘_ : Ord â†’ Ord
  âŠ”_ : {A : Set} â†’ (A â†’ Ord) â†’ Ord

data _â‰¤_ : Ord â†’ Ord â†’ Set where
  â†‘sâ‰¤â†‘s : âˆ€ {r s} â†’ r â‰¤ s â†’ â†‘ r â‰¤ â†‘ s
  sâ‰¤âŠ”f  : âˆ€ {A} {s} f (a : A) â†’ s â‰¤ f a â†’ s â‰¤ âŠ” f
  âŠ”fâ‰¤s  : âˆ€ {A} {s} f â†’ (âˆ€ (a : A) â†’ f a â‰¤ s) â†’ âŠ” f â‰¤ s
```

An ordinal is either a successor ordinal, or a limit ordinal.
(The zero ordinal could be defined as a limit ordinal.)
Intuitively, a limit ordinal `âŠ” f` is the supremum of all the ordinals returned by `f`.
This is demonstrated by the last two constructors of the preorder on ordinals:
`sâ‰¤âŠ”f` states that `âŠ” f` is an upper bound of all the ordinals of `f`,
while `âŠ”fâ‰¤s` states that it is the *least* upper bound.
Finally, `â†‘sâ‰¤â†‘s` is simply the monotonicity of taking the successor of an ordinal with respect to the preorder.
It's possible to show that `â‰¤` is indeed a preorder by proving its reflexivity and transitivity.

```
sâ‰¤s : âˆ€ {s : Ord} â†’ s â‰¤ s
sâ‰¤sâ‰¤s : âˆ€ {r s t : Ord} â†’ r â‰¤ s â†’ s â‰¤ t â†’ r â‰¤ t
```

From the preorder we define a corresponding strict order.

```
_<_ : Ord â†’ Ord â†’ Set
r < s = â†‘ r â‰¤ s
```

In a moment, we'll see that `<` can be proven to be *wellfounded*,
which is equivalent to saying that in that there are no infinite descending chains.
Obviously, for there to be no such chains, `<` must at minimum be irreflexive â€” but it's not!
There is an ordinal that is strictly less than itself,
which we'll call the "infinite" ordinal,
defined as the limit ordinal of *all* ordinals,
which is possible due to the impredicativity of `Ord`.

```
âˆ : Ord
âˆ = âŠ” (Î» s â†’ s)

âˆ<âˆ : âˆ < âˆ
âˆ<âˆ = sâ‰¤âŠ”f (Î» s â†’ s) (â†‘ âˆ) sâ‰¤s
```

To show wellfoundedness, we use an *accessibility predicate*,
whose construction for some ordinal `s` relies on showing that all smaller ordinals are also accessible.
Finally, wellfoundness is defined as a proof that *all* ordinals are accessible,
using a lemma to extract accessibility of all smaller or equal ordinals.

```
record Acc (s : Ord) : Set where
  inductive
  pattern
  constructor acc
  field
    acc< : (âˆ€ r â†’ r < s â†’ Acc r)

accessible : âˆ€ (s : Ord) â†’ Acc s
accessible (â†‘ s) = acc (Î» { r (â†‘sâ‰¤â†‘s râ‰¤s) â†’ acc (Î» t t<r â†’ (accessible s).acc< t (sâ‰¤sâ‰¤s t<r râ‰¤s)) })
accessible (âŠ” f) = acc (Î» { r (sâ‰¤âŠ”f f a r<fa) â†’ (accessible (f a)).acc< r r<fa })
```

But wait, we needed impredicativity *and* large elimination.
Where is the large elimination?

It turns out that it's hidden within Agda's pattern-matching mechanism.
Notice that in the limit case of `accessible`, we only need to handle the `sâ‰¤âŠ”f` case,
since this is the only case that could possibly apply when the left side is a successor and the right is an ordinal.
However, if you were to write this in plain CIC for instance,
you'd need to first explicitly show that the order could not be either of the other two constructors,
requiring showing that the successor and limit ordinals are provably distinct
(which itself needs large elimination, although this is permissible as an axiom),
then due to the proof architecture show that if two limit ordinals are equal, then their components are equal.
This is known as *injectivity of constructors*.
Expressing this property for ordinals requires large elimination,
since the first (implicit) argument of limit ordinals are in `Set`.

You can see how it works explicitly by writing the same proof in Coq,
where the above steps correspond to inversion followed by dependent destruction,
then printing out the full term.
The `sâ‰¤âŠ”f` subcase of the `âŠ” f` case alone spans 50 lines!

In any case, we proceed to actually deriving the inconsistency, which is easy:
show that `âˆ` is in fact *not* accessible using `âˆ<âˆ`,
then derive falsehood directly.

```
Â¬accessibleâˆ : Acc âˆ â†’ âŠ¥
Â¬accessibleâˆ (acc p) = Â¬accessibleâˆ (p âˆ âˆ<âˆ)

ng : âŠ¥
ng = Â¬accessibleâˆ (accessible âˆ)
```

The complete Agda proof can be found at [Ordinals.html](/assets/agda/Ordinals.html),
while a partial Coq proof of accessibility of ordinals can be found at [Ordinals.html](/assets/coq/Ordinals.html).

## Summary

The combinations of features that yield inconsistencies are:

* Type-in-type: `Â· âŠ¢ Set : Set`
* Impredicative `Set` and `Setâ‚` where `Â· âŠ¢ Set : Setâ‚`
* Strong impredicative pairs
* Impredicative inductive types + unrestricted large elimination
* Negative inductive types
* Non-strictly-positive inductive types + impredicativity
* Impredicativity + excluded middle + unrestricted large elimination
* Impredicative inductive types + unrestricted large elimination (again)

## Source Files

<p></p>
<details>
  <summary>Hurkens' paradox using type-in-type: <a href="/assets/agda/Hurkens.html">Hurkens.html</a></summary>
  <iframe src="/assets/agda/Hurkens.html" width="100%"></iframe>
</details>
<p></p>
<details>
  <summary>Hurkens' paradox using <code>Lower</code>: <a href="/assets/agda/HurkensLower.html">HurkensLower.html</a></summary>
  <iframe src="/assets/agda/HurkensLower.html" width="100%"></iframe>
</details>
<p></p>
<details>
  <summary>Russell's paradox using a positive inductive type and impredicative pairs: <a href="/assets/agda/Positivity.html">Positivity.html</a></summary>
  <iframe src="/assets/agda/Positivity.html" width="100%"></iframe>
</details>
<details>
  <summary>Berardi's paradox using impredicativity, excluded middle, and large elimination: <a href="/assets/agda/Berardi.html">Berardi.html</a></summary>
  <iframe src="/assets/agda/Berardi.html" width="100%"></iframe>
</details>
<details>
  <summary>Nonwellfoundedness of impredicative ordinals: <a href="/assets/agda/Ordinals.html">Ordinals.html</a></summary>
  <iframe src="/assets/agda/Ordinals.html" width="100%"></iframe>
</details>
<details>
  <summary>Accessibility of ordinals: <a href="/assets/coq/Ordinals.html">Ordinals.html</a></summary>
  <iframe src="/assets/coq/Ordinals.html" width="100%"></iframe>
</details>

<script>
  let details = document.querySelectorAll("details");
  details.forEach((detail) => {
    detail.hasBeenExpanded = false;
    detail.addEventListener("toggle", () => {
      if (!detail.hasBeenExpanded) {
        detail.hasBeenExpanded = true;
        let iframe = detail.getElementsByTagName("iframe")[0];
        let offset = iframe.src.includes("agda") ? 28 : 4; // Experimentally determined
        iframe.height = iframe.contentDocument.body.scrollHeight + offset + "px";
      }
    });
  });
</script>

<style>
#references + p {
  text-align: left;
  font-size: smaller;
}
</style>

## References

[<a name="1">1</a>] Hurkens, Antonius J. C. (TLCA 1995). _A Simplification of Girard's Paradox_. á´…á´Éª:[10.1007/BFb0014058](https://doi.org/10.1007/BFb0014058).
<br/>
[<a name="2">2</a>] Coquand, Thierry. (INRIA 1986). _An Analysis of Girard's Paradox_. [https://hal.inria.fr/inria-00076023](https://hal.inria.fr/inria-00076023).
<br/>
[<a name="3">3</a>] Buraliâ€“Forti, Cesare. (RCMP 1897). _Una questione sui numeri transfini_. á´…á´Éª:[10.1007/BF03015911](https://doi.org/10.1007/BF03015911).
<br/>
[<a name="4">4</a>] Gilbert, GaÃ«tan; Cockx, Jesper; Sozeau, Matthieu; Tabareau, Nicolas. (POPL 2019). _Definitional Proof-Irrelevance without K_. á´…á´Éª:[10.1145/3290316](https://doi.org/10.1145/3290316).
<br/>
[<a name="5">5</a>] Coquand, Theirry; Paulin, Christine. (COLOG 1988). _Inductively defined types_. á´…á´Éª:[10.1007/3-540-52335-9\_47](https://doi.org/10.1007/3-540-52335-9_47).
<br/>
[<a name="6">6</a>] Barbanera, Franco; Berardi, Stefano. (JFP 1996). _Proof-irrelevance out of excluded middle and choice in the calculus of constructions_. á´…á´Éª:[10.1017/S0956796800001829](https://doi.org/10.1017/S0956796800001829).
<br/>
[<a name="7">7</a>] Pfenning, Frank; Christine, Paulin-Mohring. (MFPS 1989). _Inductively defined types in the Calculus of Constructions_. á´…á´Éª:[10.1007/BFb0040259](https://doi.org/10.1007/BFb0040259).